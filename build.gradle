plugins {
    id 'java-gradle-plugin'
    id 'groovy'
    id 'com.gradle.plugin-publish' version '0.9.7'
}

group 'org.m2ci.msp'
version '0.2-SNAPSHOT'

repositories {
    jcenter()
    maven {
        url 'https://plugins.gradle.org/m2'
    }
}

dependencies {
    ['mac', 'linux', 'windows', 'windows64'].each { platform ->
        compile group: 'gradle.plugin.org.m2ci.msp', name: platform, version: '0.3'
    }
    testCompile group: 'org.testng', name: 'testng', version: '6.9.10'
}

configurations.all {
    resolutionStrategy.eachDependency { DependencyResolveDetails details ->
        if (details.requested.name == 'praat-wrapper') {
            details.useTarget group: "gradle.plugin.${details.requested.group}", name: details.requested.name, version: details.requested.version
        }
    }
}

gradlePlugin {
    plugins {
        praatPlugin {
            id = 'org.m2ci.msp.praat'
            implementationClass = 'org.m2ci.msp.praat.PraatPlugin'
        }
    }
}

test {
    useTestNG()
    testLogging {
        exceptionFormat = 'full'
        showStandardStreams = true
    }
}

pluginBundle {
    website = 'https://github.com/m2ci-msp/gradle-praat-plugin'
    vcsUrl = 'https://github.com/m2ci-msp/gradle-praat-plugin'
    description = 'Praat Superpowers for Gradle'
    tags = ['phonetics', 'speech science']

    plugins {
        praatPlugin {
            id = 'org.m2ci.msp.praat'
            displayName = 'Gradle Praat plugin'
        }
    }
}
